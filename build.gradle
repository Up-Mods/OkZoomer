plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.1.2'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
group = project.maven_group

if (System.env.GITHUB_ACTIONS == "true") {
	version = "${project.mod_version}-github.${System.env.GITHUB_RUN_NUMBER}"
} else {
	version = project.mod_version
}

repositories {
	maven { url "https://jitpack.io" }
	maven { url "https://maven.gegy.dev" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://aperlambda.github.io/maven" }
	// maven { url "https://maven.terraformersmc.com/releases/" }
	// The above Maven repository currently breaks down a lot, the Quilt repo has Mod Menu
	maven { url "https://maven.quiltmc.org/repository/release" }
}

dependencies {
	// To change the versions, see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "org.quiltmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.github.EnnuiL:LibZoomer:${project.libzoomer_version}"
	modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
	modImplementation "dev.lambdaurora:spruceui:${project.spruceui_version}"
	modImplementation "me.zeroeightsix:fiber:${project.fiber_version}"
	
	include "com.github.EnnuiL:LibZoomer:${project.libzoomer_version}"
	include "dev.lambdaurora:spruceui:${project.spruceui_version}"
    include "me.zeroeightsix:fiber:${project.fiber_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Ensure that the encoding is set to UTF-8, no matter what the system default is.
	// This fixes some edge cases with special characters not displaying correctly.
	// See http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 16
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from "LICENSE"
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// Add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}