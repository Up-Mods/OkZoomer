plugins {
	id 'fabric-loom' version '0.2.7-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name

if (System.env.ONE_PLUS_ONE == "TWO") {
	def branch
	branch = System.env.GITHUB_REF
	branch = branch.substring(branch.lastIndexOf("/")+1)

	version = "${project.mod_version}.${branch}+${System.env.GITHUB_RUN_NUMBER}"
} else {
	version = project.mod_version
}

group = project.maven_group

minecraft {
}

repositories {
	mavenCentral()
}

dependencies {
	//to change the versions, see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "io.github.prospector:modmenu:${project.mod_menu_version}"
	modImplementation "me.shedaniel.cloth:config-2:${project.cloth_config_version}"
	modImplementation "me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}"
	
	include "me.shedaniel.cloth:config-2:${project.cloth_config_version}"
    include "me.sargunvohra.mcmods:autoconfig1u:${project.auto_config_version}"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	repositories {
		maven {
		    name = "GitHubPackages"
			// crid | replace the link with the correct repository
            url = uri("https://maven.pkg.github.com/joaoh1/OkZoomer")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
		}
		// uncomment to publish to the local maven
		// mavenLocal()
	}

	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}
}
